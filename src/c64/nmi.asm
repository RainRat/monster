.include "macros.inc"

.BSS
nmis_disabled: .byte 0

.CODE

;*******************************************************************************
; ENABLE
; Reenables NMI's by acking the one that was held in an unacknowledged state
.export __nmi_enable
.proc __nmi_enable
	lda nmis_disabled
	beq @done

	IO_BEGIN
	lda #$7f
	sta $dd0d
	bit $dd0d	; ACK NMI
	IO_DONE

	lda #$00
	sta nmis_disabled

@done:	rts
.endproc

;*******************************************************************************
; DISABLE
; Disables NMI's from being generated by generating and not ack'ing an NMI
.export __nmi_disable
.proc __nmi_disable
	lda nmis_disabled
	bne @done
	inc nmis_disabled

	IO_BEGIN

	; save the existing NMI handler
	lda $0318
	pha
	lda $0319
	pha

        lda #<@nmi
        sta $0318
	sta $fffa
        lda #>@nmi
        sta $0319
	sta $fffb

	; generate an NMI with timer A
        lda #$00
        sta $dd0e
        sta $dd04
        sta $dd05

        lda #$81
        sta $dd0d	; set NMI source as timer A
        lda #$01
        sta $dd0e	; start timer

	pla
	sta $0318+1
	sta $fffa+1
	pla
	sta $0318
	sta $fffa

	IO_DONE

@done:	rts

@nmi:	rti
.endproc
